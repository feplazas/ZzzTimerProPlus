plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.devtools.ksp'
    id 'io.gitlab.arturbosch.detekt'
}

android {
    namespace 'com.felipeplazas.zzztimerpro'
    compileSdk 36

    defaultConfig {
        applicationId "com.felipeplazas.zzztimerpro"
        minSdk 26
        targetSdk 36
        versionCode 2
        versionName "1.1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            def keystoreProperties = new Properties()
            def keystoreFile = file("keystore.properties")
            if (keystoreFile.exists()) {
                keystoreProperties.load(new FileInputStream(keystoreFile))
                storeFile = file(keystoreProperties['storeFile'])
                storePassword = keystoreProperties['storePassword']
                keyAlias = keystoreProperties['keyAlias']
                keyPassword = keystoreProperties['keyPassword']
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            shrinkResources false
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        viewBinding true
        dataBinding false
        buildConfig true
    }

    // Fix for duplicate classes
    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES', 'META-INF/LICENSE', 'META-INF/LICENSE.txt', 'META-INF/license.txt', 'META-INF/NOTICE', 'META-INF/NOTICE.txt', 'META-INF/notice.txt', 'META-INF/ASL2.0']
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

configurations.configureEach {
    resolutionStrategy {
        // Force AndroidX
        force 'androidx.core:core:1.17.0'
        force 'androidx.appcompat:appcompat:1.7.1'
    }
}

dependencies {
    // Core Android
    implementation("androidx.core:core-ktx:1.17.0")
    implementation("androidx.appcompat:appcompat:1.7.1")
    implementation("com.google.android.material:material:1.13.0")
    implementation("androidx.constraintlayout:constraintlayout:2.2.1")

    // Lifecycle
    implementation("androidx.lifecycle:lifecycle-viewmodel-ktx:2.10.0")
    implementation("androidx.lifecycle:lifecycle-livedata-ktx:2.10.0")
    implementation("androidx.lifecycle:lifecycle-runtime-ktx:2.10.0")

    // Navigation
    implementation("androidx.navigation:navigation-fragment-ktx:2.9.6")
    implementation("androidx.navigation:navigation-ui-ktx:2.9.6")

    // Room Database
    implementation("androidx.room:room-runtime:2.8.4")
    implementation("androidx.room:room-ktx:2.8.4")
    ksp("androidx.room:room-compiler:2.8.4")

    // Coroutines
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:1.10.2")

    // Preferences
    implementation("androidx.preference:preference-ktx:1.2.1")

    // Jetpack DataStore for app settings
    implementation("androidx.datastore:datastore-preferences:1.2.0")

    // Charts for statistics
    implementation("com.github.PhilJay:MPAndroidChart:v3.1.0")

    // Google Play Billing (Removed for paid app)
    // implementation("com.android.billingclient:billing:8.3.0")
    // implementation("com.android.billingclient:billing-ktx:8.3.0")

    // WorkManager for background tasks
    implementation("androidx.work:work-runtime-ktx:2.11.0")

    // Google Play Services for location and sensors
    implementation("com.google.android.gms:play-services-location:21.3.0")

    // Health Connect for Google Fit integration
    implementation("androidx.health.connect:connect-client:1.1.0")

    // Firebase for cloud backup (comentado hasta configurar google-services.json y plugin)
    // implementation(platform("com.google.firebase:firebase-bom:34.5.0"))
    // implementation("com.google.firebase:firebase-storage-ktx")
    // implementation("com.google.firebase:firebase-auth-ktx")

    // Gson for JSON parsing
    implementation("com.google.code.gson:gson:2.13.2")

    // Material Calendar View - exclude old support library
    implementation("com.github.prolificinteractive:material-calendarview:2.0.1") {
        exclude group: 'com.android.support'
    }

    // Lottie for animations
    implementation("com.airbnb.android:lottie:6.7.1")

    // ExoPlayer for gapless audio looping
    implementation("androidx.media3:media3-exoplayer:1.5.1")

    // Testing
    testImplementation("junit:junit:4.13.2")
    androidTestImplementation("androidx.test.ext:junit:1.3.0")
    androidTestImplementation("androidx.test.espresso:espresso-core:3.7.0")
    androidTestImplementation("androidx.room:room-testing:2.8.4")
    testImplementation("androidx.test:core:1.7.0")
    testImplementation("org.robolectric:robolectric:4.16")
}

detekt {
    buildUponDefaultConfig = true
    allRules = false
    autoCorrect = true

    config = files("detekt.yml")

    reports {
        xml.required.set(true)
        html.required.set(true)
        txt.required.set(false)
    }
}
